plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def shortHeadVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        standardOutput = stdout
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
    }
    exec {
        standardOutput = stdout
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
    }
    return stdout.toString().replaceAll("[\\n\\r\\s]+", "-").trim().replaceAll("\\-+\$", "")
}()

android {
    namespace 'com.swiftkey.cornedbeef'
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 33
        versionName shortHeadVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.6.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:3.0'
    testImplementation 'org.mockito:mockito-core:2.28.2'
}

afterEvaluate {
    publishing {
        publications {
            mavenAar(MavenPublication) {
                from components.release
                groupId = 'com.touchtype'
                artifactId = project.getName()
                version = android.defaultConfig.versionName
            }
        }
    }
}